apply from: 'http://dl.bintray.com/content/shemnon/javafx-gradle/0.3.0/javafx.plugin'
apply plugin: 'java'

group = 'com.github.acisternino.tidofx'

version = '0.0.5'

sourceCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile     'org.jsoup:jsoup:1.7.+'
    compile     files( 'libs/javafx-dialogs-0.0.3.jar' )

    testCompile 'junit:junit:4.+'
    testCompile 'com.googlecode.jmockit:jmockit:1.+'
}

javafx {
    appID 'TiDoFx'
    appName 'Ticket Downloader'

    mainClass = 'tido.App'

    systemProperties = [ 'java.util.logging.config.file': 'logging.properties' ]
    jvmArgs = [ '-client' ]

    profiles {
        // not every possible platform override, but ones that have a known impact
        windows {
            id = '45795590-86a0-45d9-a748-97547058303f'
        }
        linux {
            // linux doesn't care about id, it goes un-used at the moment
            category = 'Development'
            // good luck finding a standard location for the JDK
            javaRuntime = '/opt/jdk1.7.0_40'
        }
    }

    copyright = 'Copyright (c) 2013 Andrea Cisternino'
    description = 'A simple JavaFX application to download ticket attachments from a TeamForge server.'
    licenseType = 'Apache v2.0'
    vendor = 'Andrea Cisternino'

    // icons go in src/deploy/package
    icons {
        shortcut = [
                'icon-16.png',
                'icon-32.png',
                'icon-64.png',
                'icon-128.png',
                'icon-256.png'
        ]
        volume = shortcut
        setup = 'icon-32.png'
    }
}

task distribZip(type: Zip, dependsOn: jfxDeploy) {
    description = 'Assembles a zip file for the complete bundle.'

    from "${buildDir}/distributions/bundles/${javafx.appName}"
    from( 'logging.properties' ) {
        into 'app'
    }
    from( 'LICENSE', 'README.md' )
}

task wrapper(type: Wrapper) {
    description = 'Installs supports for the Gradle Wrapper in this project.'
    gradleVersion = '1.8'
}

tasks.withType(Compile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

test {
    maxHeapSize = "512m"
}
